#!/usr/bin/env perl

# Author Nigel Horne: njh@bandsman.co.uk
# Copyright (C) 2016-2019, Nigel Horne

# Usage is subject to licence terms.
# The licence terms of this software are as follows:
# Personal single user, single computer use: GPL2
# All other users (including Commercial, Charity, Educational, Government)
#	must apply in writing for a licence for use from Nigel Horne at the
#	above e-mail.

# Generate a desendent family-tree.js file for the given person from the given
#	Gedcom

# FIXME: Show all marriages
# TODO: Only show first names of direct descendents. It will help with space.

# -l: include living people
use strict;
use warnings;
use autodie qw(:all);
# use diagnostics;
# use warnings::unused;

use Gedcom;
use Getopt::Std;
use Lingua::EN::NameCase;
use Lingua::EN::NameParse;
no lib '.';

my %opts;
getopts('lm:p:', \%opts);

die "Usage: $0 [ -l ] [ -m max-generations ] -p person-to-print filename\n" unless($ARGV[0]);

die "-p is not optional" unless($opts{p});

my $ged = Gedcom->new(gedcom_file => $ARGV[0], read_only => 1);

my $home;
my $nameparser = Lingua::EN::NameParse->new(extended_titles => 1, initials => 1);
my @rc = $ged->get_individual($opts{p});

if(scalar(@rc) == 0) {
	die "$0: Can't find '$opts{p}' in $ARGV[0]";
} elsif(scalar(@rc) == 1) {
	$home = $rc[0];
} else {
	my $i = 0;
	print STDERR join("\n", map { $i++; "$i: " . $_->as_string({ include_years => 1, middle_names => 1 }) } @rc),
		"\nMore than one $opts{p} found - choose a line number: ";
	$i = <STDIN>;
	chomp $i;
	$home = $rc[$i - 1];
	die "Incorrect line number" unless($home);
}

print "var family_tree =\n";
print_person(person => $home);
print "}\n";

sub print_person
{
	my %params = (ref($_[0]) eq 'HASH') ? %{$_[0]} : @_;

	# Gather the information on this person
	my $person = $params{'person'} or die 'Who do you want to print?';
	my $level = $params{'level'} || 1;

	return if($opts{'m'} && ($level > $opts{'m'}));

	unless($opts{'l'}) {
		return unless($person->get_value('death date'));
	}

	# print "\t" x $level,
		# '{"name": "', $person->as_string(use_aka => 1), '"';
	my $name;
	my $n2 = $person->tag_record('NAME', 2);
	if(defined($n2) && (ref($n2) eq 'Gedcom::Record') && defined($n2->type()) && ($n2->type() eq 'AlsoKnownAs')) {
		$name = $n2->value();
		if($name =~ /^(\w+)/) {
			$name = $1;
		}
	} elsif($level == 1) {
		$name = $person->as_string(use_aka => 1);
	} else {
		$name = $person->given_names();
	}
	print "\t" x $level, "{\"name\": \"$name\"";

	# Choose which spouse to print.  If all children are from one parent, choose that
	# one, otherwise choose the last.
	# There seems to be no way to display all the spouses
	my @spouses = $person->spouse();
	my %childrenbyspouse;
	my $childrenseen = 0;
	my $unknown;
	my $sex = $person->sex();
	my $spouse;
	foreach my $family ($person->fams()) {
		if($family->number_of_children() &&
		   ($spouse = ($sex eq 'M') ? $family->wife() : $family->husband())) {
			my $spouse = ($sex eq 'M') ? $family->wife() : $family->husband();
			foreach my $child($person->children()) {
				$unknown = $child;
				foreach my $spouseschild($spouse->children()) {
					if($spouseschild eq $child) {
						$childrenbyspouse{$spouse}++;
						$childrenseen++;
						$unknown = undef;
						last;
					}
				}
			}
		}
	}
	my $parentofall;
	foreach $spouse(@spouses) {
		if($parentofall && $childrenbyspouse{$spouse}) {
			$parentofall = undef;
			last;
		}
		if($childrenbyspouse{$spouse}) {
			$parentofall = $spouse;
		}
	}
	if($parentofall) {
		$spouse = $parentofall;
	} else {
		$spouse = pop @spouses;
	}
	if($spouse) {
		print ', "spouse": "', $spouse->as_string(use_aka => 1), '"';
	}

	# my @children;
	# foreach my $f($person->fams()) {
		# @children = (@children, $f->children());
	# }
	my @children = map { $_->children() } $person->fams();
	if(scalar(@children)) {
		print ', "children": [', "\n";
		foreach my $child(@children) {
			print_person({ person => $child, level => $level + 1 });
		}
		if($level == 1) {
			print "\t]\n";
		} else {
			print "\t" x $level, ']},', "\n";
		}
	} elsif($level > 1) {
		print "},\n";
	} else {
		print "\n";
	}
}

sub Gedcom::Individual::as_string
{
	my $self = shift;
	my %args = (ref($_[0]) eq 'HASH') ? %{$_[0]} : @_;

	my $name;

	if($args{'use_aka'}) {
		my $n2 = $self->tag_record('NAME', 2);
		if(defined($n2) && (ref($n2) eq 'Gedcom::Record') && defined($n2->type()) && ($n2->type() eq 'AlsoKnownAs')) {
			$name = $n2->value();
		} else {
			$name = $self->name();
		}
	} else {
		$name = $self->name();
	}

	$name =~ s/\///g;
	$nameparser->parse($name);

	my %name_components = $nameparser->components();
	my $first_name = $name_components{'given_name_1'};
	my $middle_name = $name_components{'middle_name'};
	my $surname = $name_components{'surname_1'};
	if($name_components{'surname_2'}) {
		$surname .= ' ' . $name_components{'surname_2'};
	}
	if($name_components{'non_matching'}) {
		if($name =~ /(\w{2,})\s(\w{2,})\s(\w+)\s(\w{2,})/) {
			# Name parser doesn't parse names like this very well
			$first_name = $1;
			$middle_name = "$2 $3";
			$surname = $4;
		} else {
			if($surname) {
				$surname .= ' ';
			}
			my $rest = $name_components{'non_matching'};
			if($rest =~ /(.+) $/) {
				$rest = $1;
			}
			$surname .= $rest;
		}
	} elsif(!defined($surname)) {
		$surname = $self->surname();
	}

	my $has_maiden_name;
	if($args{'nee'}) {
		my $sex = $self->get_value('sex');
		if(defined($sex) && ($sex eq 'F')) {
			if(my $husband = $self->husband()) {
				$surname = $husband->surname();
				$has_maiden_name = 1;
			} elsif(my $spouse = $self->spouse()) {
				warn $self->as_string(), 'married, but no husband relationship';
				$surname = $spouse->surname();
				$has_maiden_name = 1;
			}
		}
	}

	my $rc;

	if($args{'title'}) {
		$rc = $name_components{'title_1'};
		if($rc) {
			$rc .= ' ';
		}
	}

	if($first_name) {
		$rc .= $first_name;
	} else {
		$rc .= $self->given_names();
	}

	if($args{'middle_names'}) {
		if($middle_name) {
			$rc .= " $middle_name";
		} elsif($name =~ /\s([A-Z])\s.+/) {
			# Just an initial has been given
			$rc .= " $1";
		}
	}

	if($surname) {
		if($rc) {
			$rc .= ' ';
		}
		$rc .= normalize_name($surname);
		if(my $suffix = $name_components{'suffix'}) {
			$rc .= " $suffix";
		}
	}

	if($has_maiden_name && $self->surname()) {
		$rc .= ' (nee ' . normalize_name($self->surname()) . ')';
	}

	if(!defined($rc)) {
		if($opts{'f'}) {
			die "Can't determine name of this person";
		}
		if($opts{'w'}) {
			warn "Can't determine name of this person";

			$args{'include_years'} = 1;
			$rc = 'Unknown person ';
		}
	}
	if($args{'include_years'}) {
		my $dob = $self->get_value('birth date');

		my $yob;	# Year of birth
		if($dob && ($dob =~ /.*?(\d{3,4})/)) {
			if($dob =~ /^(Abt|ca?)\.?\s*(.+)/i) {
				$yob = "c$2";
			} else {
				$yob = $1;
			}
		} else {
			my $dob = $self->get_value('baptism date');
			if($dob && ($dob =~ /.*?(\d{3,4})/)) {
				$yob = "c$1";
			}
		}

		my $dod = $self->get_value('death date');

		my $yod;	# Year of death
		if($dod && ($dod =~ /.*?(\d{3,4})/)) {
			$yod = $1;
		} else {
			my $dod = $self->get_value('burial date');
			if($dod && ($dod =~ /.*?(\d{3,4})/)) {
				$yod = "c$1";
			}
		}

		$rc .= ' ' if($yob || $yod);

		if($yob) {
			$rc .= "$yob ";
		}

		$rc .= '-' if($yob || $yod);

		if($yod) {
			return "$rc $yod";
		}
	}

	return $rc;
}

sub normalize_name
{
	# my $name = shift;

	# my $rc;
	# foreach my $word(split(/-| |'/, lc($name))) {
		# $rc .= '-' if($rc && ($name =~ /-/));
		# $rc .= "'" if($rc && ($name =~ /'/));
		# $rc .= ' ' if($rc && ($name =~ / /));
		# $rc .= ucfirst($word);
	# }

	# return $rc;

	return Lingua::EN::NameCase::nc(shift);
}
